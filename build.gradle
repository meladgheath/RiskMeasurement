plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

def VERSION = '0.1.1'
def NAME = 'casher-engine'
def JAVAFX_VERSION = '17.0.2'
def MAIN_PACKAGE = 'org.softwareengine'
def MAIN_CLASS = MAIN_PACKAGE + '.Main'
def MAIN_MODULE = 'casher'

compileJava.options.encoding = 'UTF-8'

javafx {
    version = JAVAFX_VERSION
    modules = ['javafx.graphics', 'javafx.controls']
}

application {
    group = MAIN_PACKAGE
    version = VERSION
    applicationName = NAME
    mainModule = MAIN_MODULE
    mainClass = MAIN_CLASS
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    implementation 'org.postgresql:postgresql:42.5.1'
    implementation 'net.sf.jasperreports:jasperreports:6.20.0'
    implementation 'com.lowagie:itext:4.2.2'
}

jar {
    manifest {
        attributes 'Main-Class': MAIN_CLASS
    }
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = NAME
    }
    jpackage {
        appVersion = VERSION
        vendor = 'Software Engine Foundation'
        installerOptions = [
                '--icon', 'src/main/resources/icons/app.ico',
                '--type', 'msi',
                '--win-menu',
                '--win-shortcut',
                '--copyright', 'Copyright, All rights reserved',
                '--win-upgrade-uuid', '123e4567-e89b-12d3-a456-426655440000',
        ]
    }
}

test {
    useJUnitPlatform()
}